buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
   dependencies {
       classpath('io.swagger:swagger-codegen:2.2.3')
       classpath('com.github.jengelman.gradle.plugins:shadow:2.0.2')
   }
}

plugins {
    id 'idea'
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'org.ajoberstar.grgit' version '2.2.1'
}

import org.ajoberstar.grgit.Grgit

apply from: 'versioning.gradle'

group = applicationGroupId

def codegenOutputDir = "generated"
def codegenSrcDir = codegenOutputDir + "/src/main/java"
def codegenResourceDir = codegenOutputDir + "/src/main/resources"

configurations {
    all {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
        }

        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
        exclude group: "org.eclipse.jetty", module: "jetty-server"
    }

    swaggerCodegen
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://artifactory.elyxor.com/libs-release" }
    jcenter()
}

dependencies {
    swaggerCodegen 'io.swagger:swagger-codegen:2.2.3'

    compile 'io.swagger:swagger-annotations:1.5.21'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.3'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'joda-time:joda-time:2.10'

    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs = [ codegenSrcDir ]
        }
        resources {
            srcDirs = [ codegenResourceDir ]
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    baseName = project.name
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = project.name
    from javadoc.destinationDir
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext {
    startTime = new Date().format("yyy-MM-dd'T'HH:mm:ssZ")

    def gitRepo = Grgit.open(dir: projectDir.toString())
    gitHash = gitRepo.head().id
    gitBranch = gitRepo.branch.current().name
    gitRepo.close()

    currentJvm = org.gradle.internal.jvm.Jvm.current()

    if (gitBranch == 'master') {
        println 'Building master branch'
        project.version = getMasterVersion()
    } else {
        branchVersionIdentifier = gitBranch.replaceAll("[\\W]", "_")
        println 'Building branch: ' + branchVersionIdentifier
        project.version = getBranchVersion(branchVersionIdentifier)
    }

    println 'Version: ' + project.version
}

task setReleaseVersion {
    doFirst {
        project.version = project.version.replaceAll('-SNAPSHOT', '')
    }
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        CachethqSwagger(MavenPublication) {
            artifact jar
            artifact sourceJar
            artifact javadocJar
            version project.version
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.applicationTitle
                    description project.applicationDescription
                    url project.url
                    scm {
                        url project.url
                        connection project.scmUrl
                        developerConnection project.scmUrl
                    }
                    licenses {
                        license {
                            name project.licenseName
                            url project.licenseUrl
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

import io.swagger.codegen.config.CodegenConfigurator
import io.swagger.codegen.DefaultGenerator

task generateApi {
    FileTree swaggerFiles = fileTree('.') {
        include '*.yaml'
    }

    inputs.files(swaggerFiles)
    outputs.dir(codegenOutputDir)

    doLast {
        swaggerFiles.each { File file ->
            println "Processing file: " + file
            def config = new CodegenConfigurator()
            config.setInputSpec(file.toString())
            config.setOutputDir(codegenOutputDir)
            config.setLang('java')
            config.setLibrary('okhttp-gson')
            config.setAdditionalProperties([
                    'apiPackage'  : 'com.elyxor.cachethq.api',
                    'modelPackage': 'com.elyxor.cachethq.model',
                    'sourceFolder': 'src/main/java'
            ])
            new DefaultGenerator().opts(config.toClientOptInput()).generate()
        }
    }
}

compileJava.dependsOn generateApi

clean.doFirst {
    delete(codegenOutputDir)
}
