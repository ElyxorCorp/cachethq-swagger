buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
   dependencies {
       classpath('io.swagger.codegen.v3:swagger-codegen:3.0.52')
       classpath('com.github.jengelman.gradle.plugins:shadow:2.0.2')
       classpath('io.swagger.codegen.v3:swagger-codegen-generators:1.0.46')
   }
}

plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.ajoberstar.grgit' version '2.2.1'
    id "org.openapi.generator" version "7.2.0"
}

import org.ajoberstar.grgit.Grgit

apply from: 'versioning.gradle'

group = applicationGroupId

def codegenOutputDir = "generated"
def codegenSrcDir = codegenOutputDir + "/src/main/java"
def codegenResourceDir = codegenOutputDir + "/src/main/resources"

configurations {
    all {
        resolutionStrategy {
            cacheDynamicVersionsFor 0, 'seconds'
        }

        exclude group: "org.slf4j", module: "slf4j-log4j12"
        exclude group: "log4j", module: "log4j"
        exclude group: "org.eclipse.jetty", module: "jetty-server"
    }

    swaggerCodegen
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen:3.0.52'
    
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'joda-time:joda-time:2.10'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDirs = [ codegenSrcDir ]
        }
        resources {
            srcDirs = [ codegenResourceDir ]
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    baseName = project.name
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = project.name
    from javadoc.destinationDir
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

ext {
    startTime = new Date().format("yyy-MM-dd'T'HH:mm:ssZ")

    def gitRepo = Grgit.open(dir: projectDir.toString())
    gitHash = gitRepo.head().id
    gitBranch = gitRepo.branch.current().name
    gitRepo.close()

    currentJvm = org.gradle.internal.jvm.Jvm.current()

    if (gitBranch == 'master') {
        println 'Building master branch'
        project.version = getMasterVersion()
        artifactoryRepo = 'libs-release-local'
    } else {
        branchVersionIdentifier = gitBranch.replaceAll("[\\W]", "_")
        println 'Building branch: ' + branchVersionIdentifier
        project.version = getBranchVersion(branchVersionIdentifier)
        artifactoryRepo = 'libs-snapshot-local'
    }

    println 'Version: ' + project.version
}

task setReleaseVersion {
    doFirst {
        project.version = project.version.replaceAll('-SNAPSHOT', '')
    }
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        CachethqSwagger(MavenPublication) {
            artifact jar
            artifact sourceJar
            artifact javadocJar
            version project.version
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.applicationTitle
                    description project.applicationDescription
                    url project.url
                    scm {
                        url project.url
                        connection project.scmUrl
                        developerConnection project.scmUrl
                    }
                    licenses {
                        license {
                            name project.licenseName
                            url project.licenseUrl
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven { 
            url = System.getenv('URL') + '/artifactory/' + artifactoryRepo
            credentials(HttpHeaderCredentials) {
                name = "X-JFrog-Art-API"
                value = System.getenv('TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

import io.swagger.codegen.v3.config.CodegenConfigurator
import io.swagger.codegen.v3.DefaultGenerator
import io.swagger.codegen.v3.CodegenConfigLoader
import io.swagger.codegen.v3.ClientOptInput
import io.swagger.codegen.v3.ClientOpts
import io.swagger.v3.parser.OpenAPIV3Parser

task copyPyProjectFile(type: Copy, dependsOn: classes) {
    println "Copy pyproject.toml"
    from "${rootProject.projectDir}/pyproject.toml"
    into 'python-client'
}

openApiGenerate {
    generatorName.set("python-pydantic-v1")
    inputSpec.set("$rootDir/swagger.yaml")
    outputDir.set("$rootDir/python-client")
    packageName.set("cachethq_client")
}

task generateApi {
    FileTree swaggerFiles = fileTree('.') {
        include '*.yaml'
    }

    inputs.files(swaggerFiles)
    outputs.dir('generated/src/main/java/com/elyxor/cachethq/model')
    outputs.dir('generated/src/main/java/com/elyxor/cachethq/api')
    outputs.dir('generated/src/test/java/com/elyxor/cachethq/api')
    outputs.dir('generated/gradle/wrapper')

    doLast {
        swaggerFiles.each { File file ->
            println "Processing file: " + file            
            def codegenConfig = CodegenConfigLoader.forName('java')
            codegenConfig.setOutputDir(codegenOutputDir)
            codegenConfig.setLibrary('okhttp-gson')
            def options = new ClientOpts()
            options.setProperties([
                    'dateLibrary': 'java8',
                    'apiPackage'  : 'com.elyxor.cachethq.api',
                    'modelPackage': 'com.elyxor.cachethq.model',
                    'sourceFolder': 'src/main/java',
                    'interfaceOnly': 'true'
            ])
            def openAPI = new OpenAPIV3Parser().read(file.toString(), null, null)
            def clientOpts = new ClientOptInput()
            clientOpts.setOpenAPI(openAPI)
            clientOpts.setConfig(codegenConfig)
            clientOpts.setOpts(options)

            new DefaultGenerator().opts(clientOpts).generate()

        }
    }
}

compileJava.dependsOn generateApi
generateApi.finalizedBy tasks.openApiGenerate
tasks.openApiGenerate.finalizedBy copyPyProjectFile

clean.doFirst {
    delete(codegenOutputDir)
    delete('python-client')
}
